//This is required as flyway now has a stand alone library for each database rather than being bundled together
buildscript {
  dependencies {
    classpath("org.flywaydb:flyway-database-postgresql:10.15.2")
  }
}

plugins {
  id 'application'
  id 'checkstyle'
  id 'io.spring.dependency-management' version '1.1.6'
  id 'jacoco'
  id 'org.springframework.boot' version '3.3.2'
  id 'org.owasp.dependencycheck' version '10.0.0'
  id 'org.sonarqube' version '5.1.0.4882'
  id 'pmd'
  id 'org.flywaydb.flyway' version '10.17.1'
  id 'io.freefair.lombok' version '8.7.1'
  id 'org.jetbrains.kotlin.jvm' version '2.0.10'
}

application {
  group = 'uk.gov.hmcts.reform.pip'
  mainClass = 'uk.gov.hmcts.reform.pip.data.management.Application'
  version = '0.0.1'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.17.0'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "7.4.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly

  all {
    exclude group: 'commons-logging', module: 'commons-logging'
  }
}

jacoco {
  toolVersion = "0.8.12"
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation.set(file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml"))
  }
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        minimum = 0.8
      }
    }
  }
}

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
  baselineVersion = '000'
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {

  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

def sonarExclusions = ["src/main/java/uk/gov/hmcts/reform/pip/data/management/config/AzureBlobConfiguration.java"]

sonarqube {
  properties {
    property "sonar.projectName", "PIP Data Management"
    property "sonar.projectKey", "pip-data-management"
    property "sonar.exclusions", sonarExclusions.join(', ')
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

ext {
  reformLoggingVersion = "6.1.6"
  log4JVersion = "2.23.1"
}

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'

  implementation group: 'com.azure', name: 'azure-storage-blob', version: '12.27.0'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'

  implementation group: 'com.azure', name: 'azure-storage-blob', version: '12.26.1'
  implementation group: 'com.jayway.jsonpath', name: 'json-path', version: '2.9.0'
  implementation group: 'io.hypersistence', name: 'hypersistence-utils-hibernate-63', version: '3.8.2'
  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.6.0'
  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: reformLoggingVersion
  implementation group: 'com.github.hmcts.java-logging', name: 'logging-appinsights', version: reformLoggingVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion

  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '8.0'
  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.4'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.7.3'

  implementation group: 'com.github.hmcts', name: 'pip-data-models', version: '2.1.30'
  implementation 'com.networknt:json-schema-validator:1.5.1'
  implementation group: 'com.google.guava', name: 'guava', version: '33.2.1-jre'
  implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.3.0'
  implementation group: 'com.openhtmltopdf', name: 'openhtmltopdf-pdfbox', version: '1.0.10'
  implementation group: 'com.openhtmltopdf', name: 'openhtmltopdf-slf4j', version: '1.0.10'
  implementation 'com.networknt:json-schema-validator:1.4.3'

  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-jdbc'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'com.azure.spring:spring-cloud-azure-starter-active-directory:5.15.0'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
  implementation 'com.opencsv:opencsv:5.9'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.springframework.retry:spring-retry:2.0.7'
  implementation("com.squareup.okhttp3:okhttp:4.12.0")

  // Include Flyway for database migrations
  implementation group: 'org.flywaydb', name: 'flyway-core', version: '10.17.1'
  implementation group: 'org.flywaydb', name: 'flyway-database-postgresql', version: '10.17.1'

  // Force upgrade snakeyaml version for CVE-2022-1471
  implementation( group: 'org.yaml', name: 'snakeyaml').version {
    strictly("2.0")
  }

  testImplementation("com.squareup.okhttp3:mockwebserver:4.12.0")
  testImplementation "io.zonky.test:embedded-database-spring-test:2.5.1"
  testImplementation(platform('org.junit:junit-bom:5.11.0'))
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation group: 'org.springframework.security', name: 'spring-security-test'
  testImplementation group: 'io.github.hakky54', name: 'logcaptor', version: '2.9.3'
  testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2'
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.4.4', classifier: 'all'
  testImplementation group: 'org.jsoup', name: 'jsoup', version: '1.18.1'
}

task fortifyScan(type: JavaExec)  {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

bootJar {
  archiveFileName = "pip-data-management.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
