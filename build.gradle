import org.flywaydb.gradle.task.FlywayMigrateTask

//This is required as flyway now has a stand alone library for each database rather than being bundled together
buildscript {
  dependencies {
    classpath("org.flywaydb:flyway-database-postgresql:11.15.0")
  }
}

plugins {
  id 'application'
  id 'uk.gov.hmcts.java' version '0.12.67'
  id 'checkstyle'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'jacoco'
  id 'org.springframework.boot' version '3.5.7'
  id 'org.owasp.dependencycheck' version '12.1.3'
  id 'org.sonarqube' version '7.0.1.6134'
  id 'pmd'
  id 'org.flywaydb.flyway' version '11.15.0'
  id 'io.freefair.lombok' version '9.0.0'
  id 'org.jetbrains.kotlin.jvm' version '2.2.21'
}

application {
  group = 'uk.gov.hmcts.reform.pip'
  mainClass = 'uk.gov.hmcts.reform.pip.data.management.Application'
  version = '0.0.1'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1'
  }
}

def configureSourceSet(String name) {
  sourceSets.create(name) { sourceSet ->
    sourceSet.java {
      compileClasspath += sourceSets.main.output + sourceSets.test.output
      runtimeClasspath += sourceSets.main.output + sourceSets.test.output
      srcDir "src/${name}/java"
    }
    sourceSet.resources.srcDir "src/${name}/resources"
  }
}

['smokeTest', 'integrationTest', 'functionalTest'].each { configureSourceSet(it) }

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

tasks.register('functional', Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.register('integration', Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
}

tasks.register('smoke', Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  toolVersion = '12.1.1'
}

pmd {
  toolVersion = "7.18.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("${layout.buildDirectory.get()}/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom functionalTestImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly

  all {
    exclude group: 'commons-logging', module: 'commons-logging'
  }
}

jacoco {
  toolVersion = "0.8.14"
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
  }
}

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        minimum = 0.8
      }
    }
  }
}

flyway {
  url = System.getenv('FLYWAY_URL')
  user = System.getenv('FLYWAY_USER')
  password = System.getenv('FLYWAY_PASSWORD')
  baselineOnMigrate = true
  baselineVersion = '000'
}

tasks.register('migratePostgresDatabase', FlywayMigrateTask) {
  baselineOnMigrate = true
  if (project.hasProperty("dburl")) {
    url = "jdbc:postgresql://${dburl}"
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

def sonarExclusions = ["src/main/java/uk/gov/hmcts/reform/pip/data/management/config/AzureBlobConfiguration.java"]

sonarqube {
  properties {
    property "sonar.projectName", "PIP Data Management"
    property "sonar.projectKey", "pip-data-management"
    property "sonar.exclusions", sonarExclusions.join(', ')
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  suppressionFile = 'config/owasp/suppressions.xml'
  skipTestGroups  = false
}

ext {
  reformLoggingVersion = "6.1.9"
  log4JVersion = "2.25.2"
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-aop'
  implementation 'org.springframework.boot:spring-boot-starter-json'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

  implementation 'com.azure:azure-storage-blob:12.32.0'
  implementation 'com.jayway.jsonpath:json-path:2.10.0'
  implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.11.0'
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'
  implementation "com.github.hmcts.java-logging:logging:${reformLoggingVersion}"
  implementation "com.github.hmcts.java-logging:logging-appinsights:${reformLoggingVersion}"
  implementation "org.apache.logging.log4j:log4j-api:${log4JVersion}"
  implementation "org.apache.logging.log4j:log4j-to-slf4j:${log4JVersion}"
  implementation 'org.postgresql:postgresql:42.7.8'
  implementation 'com.github.hmcts:pip-data-models:2.3.28'
  implementation 'com.networknt:json-schema-validator:1.5.8'
  implementation 'com.google.guava:guava:33.5.0-jre'
  implementation 'org.apache.poi:poi-ooxml:5.4.1'
  implementation 'com.openhtmltopdf:openhtmltopdf-pdfbox:1.0.10'
  implementation 'com.openhtmltopdf:openhtmltopdf-slf4j:1.0.10'
  implementation 'net.logstash.logback:logstash-logback-encoder:9.0'

  implementation 'org.springframework.boot:spring-boot-starter-validation'
  implementation 'org.springframework.boot:spring-boot-starter-jdbc'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'com.azure.spring:spring-cloud-azure-starter-active-directory:6.0.0'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
  implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
  implementation 'com.opencsv:opencsv:5.12.0'
  implementation 'org.springframework.boot:spring-boot-starter-webflux'
  implementation 'org.springframework.retry:spring-retry:2.0.12'
  implementation 'org.apache.commons:commons-lang3:3.19.0'

  // Include Flyway for database migrations
  implementation 'org.flywaydb:flyway-core:11.15.0'
  implementation 'org.flywaydb:flyway-database-postgresql:11.15.0'

  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  testImplementation("com.squareup.okhttp3:mockwebserver:5.3.0")
  testImplementation "io.zonky.test:embedded-database-spring-test:2.6.0"
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.springframework.security:spring-security-test'
  testImplementation 'io.github.hakky54:logcaptor:2.12.1'
  testImplementation 'org.apiguardian:apiguardian-api:1.1.2'
  testImplementation 'com.github.hmcts:fortify-client:1.4.10:all'
  testImplementation 'org.jsoup:jsoup:1.21.2'
  testImplementation 'com.google.code.gson:gson:2.13.2'

  functionalTestImplementation 'io.rest-assured:rest-assured'
}

tasks.register('fortifyScan', JavaExec) {
  mainClass = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

bootJar {
  archiveFileName = "pip-data-management.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processFunctionalTestResources") {
  duplicatesStrategy = 'include'
}

rootProject.tasks.named("processIntegrationTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
